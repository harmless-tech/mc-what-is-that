plugins {
	id "fabric-loom" version "1.5-SNAPSHOT"

	id "maven-publish"
	id "com.modrinth.minotaur" version "2.+"

	id "com.diffplug.spotless" version "6.25.0"
	id "com.github.spotbugs" version "6.0.8"
}

version = "fabric" + project.mod_version + "+" + project.minecraft_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
		name = "ParchmentMC"
		url = "https://maven.parchmentmc.org"
	}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"what-was-that" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

fabricApi {
	configureDataGeneration()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	//	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	//	mappings loom.officialMojangMappings()
	mappings loom.layered() {
		officialMojangMappings()
		parchment "org.parchmentmc.data:parchment-${project.minecraft_version}:${project.parchment_mappings}@zip"
	}

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	// TODO: Use subset of fabric api. (https://github.com/FabricMC/fabric-example-mod/issues/175)

	compileOnly "org.jetbrains:annotations:24.1.0"

	spotbugs "com.github.spotbugs:spotbugs:${spotbugs.toolVersion.get()}"
	spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0"
	spotbugsPlugins "com.mebigfatguy.sb-contrib:sb-contrib:7.6.4"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

build {
	dependsOn spotlessApply
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/harmless-tech/mc-what-is-that"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}

spotbugs {
	showProgress = true
	excludeFilter = file("spotbugs-exclude.xml")
}

spotbugsMain {
	reports {
		html {
			enabled = true
			stylesheet = "fancy-hist.xsl"
		}
	}
}

spotbugsClient {
	reports {
		html {
			enabled = true
			stylesheet = "fancy-hist.xsl"
		}
	}
}

spotless {
	java {
		target "src/*/java/**"

		eclipse()
		formatAnnotations()
	}
	groovy {
		target "*.gradle"

		importOrder()
		removeSemicolons()

		greclipse()
	}
	json {
		target "src/**/*.json"
		jackson()
	}
	format "xml", {
		target "*.xml"
		eclipseWtp("xml")
	}
}
